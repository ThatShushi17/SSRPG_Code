/* ====================
    ANCHOR Short Funcs
:====================== */

var Size = string.Size
var Split = string.Split
var Sub = string.Sub
var Equals = string.Equals

// a proper one this time
func Mod(n, m)
  return ((n % m) + m) % m



/* ==================
    ANCHOR Functions
:==================== */

func SetCanvasSprite(canvas, ascii)
  var lines = Split(ascii, "\n＂)

  for y = 0 .. lines.Count() - 1
    var line = lines［y］
    for x = 0 .. Size(line) - 1
      var char = Sub(line, x, 1)
        ?char ! "#"
          canvas.Set(x,y,char)

func SetCanvasGradient(canvas, color1, color2, startPosition)
  var w = canvas.w
  var h = canvas.h
  
  var start = []
  var end = []
  
  var startStrings = Split(startPosition, "_")
  var yStartPos = startStrings[0]
  var xStartPos = startStrings[1]
  ?xStartPos = "left"
    start.Add(0)
    end.Add(w - 1)
  :?xStartPos = "right"
    start.Add(w - 1)
    end.Add(0)
  :
    start.Add(null)
    end.Add(null)

  ?yStartPos = "top"
    start.Add(0)
    end.Add(h - 1)
  :?yStartPos = "bottom"
    start.Add(h - 1)
    end.Add(0)
  :
    start.Add(null)
    end.Add(null)

  var val
  var col
  for y = 0 .. h - 1
    for x = 0 .. w - 1
      ?start[0] = null & start[1] = null
        val = 0.5
      :?start[0] = null
        val = (y * 1.0) / (h - 1)
      :?start[1] = null
        val = (x * 1.0) / (w - 1)
      :
        val = ((x + y) * 1.0) / (w + h - 2)

      col = color.Lerp(color1, color2, val)

      canvas.SetFG(x, y, col)

func ApplyRainbow (canvas, colors, mask, xShear, yShear, t)
  var w = canvas.w
  var h = canvas.h
  var cols = colors
  ?cols = null
    cols = [
      #ff4994,
      #ff4973,
      #ff4a4e,
      #ff643a,
      #ff833a,
      #ff9b39,
      #ffaf3a,
      #ffc43b,
      #ffdc3c,
      #fffa40,
      #dcff42,
      #a7ff45,
      #4fff5b,
      #49ffa2,
      #44ffc7,
      #42ffe3,
      #41fffa,
      #3fefff,
      #3fddff,
      #3ecbff,
      #3db6ff,
      #3b9dff,
      #2b66ff,
      #5753ff,
      #7c50ff,
      #9f4fff,
      #c44fff,
      #ef51ff,
      #ff4ee0,
      #ff4bb7
    ]
  var count = cols.Count()

  for y = 0 .. h - 1
    var yPhase = y * yShear

    for x = 0 .. w - 1
      var i = x + y * w
      var xPhase = x * xShear

      var maskCond = Equals(mask, "1")
      ?!maskCond
        maskCond = Equals(Sub(mask, i, 1), ＂1＂)

      ?maskCond
        var phase = Mod(xPhase + yPhase + t, count)
        canvas.SetFG(x, y, cols[phase])
